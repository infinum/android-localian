buildscript {

    apply from: "config.gradle"
    apply from: "maven.gradle"
    apply from: "dependencies.gradle"

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://dl.bintray.com/infinum/android" }
    }
    dependencies {
        classpath packages.gradle
        classpath packages.kotlin.plugin
        classpath packages.cpd.plugin
        classpath packages.detekt.plugin
        classpath packages.ktlint.plugin
        classpath packages.dokka.plugin
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter() {
            content {
                includeModule("com.soywiz.korlibs.korte", "korte-jvm")
                includeModule("org.jetbrains.trove4j", "trove4j")
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
                includeGroup("org.jetbrains.dokka")
                includeGroup("org.jetbrains")
            }
        }
    }
}

subprojects {
    def buildProperties = new Properties()
    file(rootDir.absolutePath+"/build.properties").withInputStream { buildProperties.load(it) }
    if (buildProperties.getProperty("build.debug").toBoolean()) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    substitute module(packages.localian) with project(':localian')
                }
            }
        }
    }

    repositories {
        google()
        mavenCentral()
        maven { url "https://dl.bintray.com/infinum/android" }
    }

    apply from: "$rootDir/cpd.gradle"
    apply from: "$rootDir/detekt.gradle"
    apply from: "$rootDir/ktlint.gradle"
}

apply from: "deploy.gradle"
apply from: "dokka.gradle"

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task detektAll(dependsOn: [
    ':localian:detekt'
]) {
    group = "Verification"
    description = "Run detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':localian:ktlintCheck'
]) {
    group = "Verification"
    description = "Run ktlint on all modules"
}

task cpdAll(dependsOn: [
        ':localian:cpdCheck'
]) {
    group = "Verification"
    description = "Run CPD on all modules"
}

task runStaticChecks(dependsOn: [
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
]) {
    group = "Verification"
    description = "Run static checks on all modules"
}