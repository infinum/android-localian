apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
            maven {
                name github.name()
                url github.url()
                credentials {
                    username github.username()
                    password github.password()
                }
            }
            maven {
                name bintray.name()
                url bintray.url() + "/localian/;publish=1;override=1"
                credentials {
                    username bintray.username()
                    password bintray.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = releaseConfig.group
                version = releaseConfig.version

                artifactId = "localian"

                artifact bundleReleaseAar
                artifact javadocsJar
                artifact sourcesJar

                pom {
                    name = "Localian"
                    description = "Single point of entry for all Locale manipulation accross diferent API levels."
                    url = "https://github.com/infinum/android-localian"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "knobtviker"
                            name = "Bojan KomljenoviÄ‡"
                            email = "bojan.komljenovic@infinum.com"
                        }
                    }
                    scm {
                        url = "scm:git@github.com:infinum/android-localian.git"
                        connection = "scm:git@github.com:infinum/android-localian.git"
                        developerConnection = "scm:git@github.com:infinum/android-localian.git"
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode("dependencies")
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode("dependency")
                        dependencyNode.appendNode("groupId", it.group)
                        dependencyNode.appendNode("artifactId", it.name)
                        dependencyNode.appendNode("version", it.version)
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}