buildscript {

    apply from: "config.gradle"
    apply from: "maven.gradle"

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath libs.gradle
        classpath libs.kotlin.plugin
        classpath libs.cpd
        classpath libs.detekt.plugin
        classpath libs.ktlint
        classpath libs.dokka
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter() {
            content {
                includeModule("org.jetbrains.kotlinx", "kotlinx-html-jvm")
            }
        }
    }
}

subprojects {
    def buildProperties = new Properties()
    file(rootDir.absolutePath+"/build.properties").withInputStream { buildProperties.load(it) }
    if (buildProperties.getProperty("build.debug").toBoolean()) {
        configurations.all {
            resolutionStrategy {
                dependencySubstitution {
                    //substitute module(packages.localian) with project(':localian')
                }
            }
        }
    }

    repositories {
        google()
        mavenCentral()
    }

    apply from: "$rootDir/cpd.gradle"
    apply from: "$rootDir/detekt.gradle"
    apply from: "$rootDir/ktlint.gradle"
    apply from: "$rootDir/dokka.gradle"
}

apply from: "deploy.gradle"

tasks.withType(JavaCompile) {
    options.compilerArgs += ["--release", "8"]
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task lintAll(dependsOn: [
        ':localian:lintRelease'
]) {
    group = "Verification"
    description = "Run Lint on all modules"
}

task detektAll(dependsOn: [
    ':localian:detekt'
]) {
    group = "Verification"
    description = "Run Detekt on all modules"
}

task ktlintAll(dependsOn: [
        ':localian:ktlintCheck'
]) {
    group = "Verification"
    description = "Run ktlint on all modules"
}

task cpdAll(dependsOn: [
        ':localian:cpdCheck'
]) {
    group = "Verification"
    description = "Run CPD on all modules"
}

task runStaticChecks(dependsOn: [
        ':lintAll',
        ':detektAll',
        ':ktlintAll',
        ':cpdAll'
]) {
    group = "Verification"
    description = "Run static checks on all modules"
}