plugins {
    id "com.android.library"
    id "kotlin-android"
}

android {
    buildFeatures {
        buildConfig = false
        viewBinding = false
    }

    namespace = "com.infinum.localian"

    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode releaseConfig.versionCode
        versionName releaseConfig.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            debuggable true
            minifyEnabled false
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    resourcePrefix 'localian_'

    kotlin {
        jvmToolchain(8)
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    kotlinOptions {
        freeCompilerArgs += [
                '-Xexplicit-api=strict',
                '-Xjvm-default=all'
        ]
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation libs.kotlin.core
    implementation libs.bundles.androidx
    api libs.androidx.startup

    testImplementation libs.bundles.testing
}

apply from: "publish.gradle"
